#!/usr/bin/perl

use v5.38;
use integer;

@ARGV = shift // "input";

my $solution_1 = 0;
my $solution_2 = 0;

sub calc {
    my ( $type, $result, @factors ) = @_;
    my $n = @factors - 1;
    for my $ops ( 0 .. ( $type**$n ) - 1 ) {
        my ( $x, @stack ) = @factors;
        my $op = $ops;
        while ( my $y = shift @stack ) {
            my $r = $op % $type;
            $op /= $type;
            if    ( $r == 0 )    { $x += $y; }
            elsif ( $r == 1 )    { $x *= $y; }
            elsif ( $type == 3 ) { $x .= $y; }
        }
        return 1 if $x == $result;
    }
    return 0;
}

INPUT:
while (<>) {
    my ( $result, @factors ) = /\d+/g;
    $solution_1 += $result if calc( 2, $result, @factors );
    $solution_2 += $result if calc( 3, $result, @factors );
}

say "Solution 1: $solution_1";
say "Solution 2: $solution_2";

# vim:ft=perl:sts=4:sw=4:et
