#!/usr/bin/perl

use v5.38;
@ARGV = shift // "input";

my $solution_1 = 0;
my $solution_2 = 0;
my $err        = 10000000000000;

$/ = '';
my @data = map { [/\d+/g] } <>;

for (@data) {
    my ( $ax, $ay, $bx, $by, $px, $py ) = @$_;
    $solution_1 += cost( $ax, $ay, $bx, $by, $px, $py );
    $px         += $err;
    $py         += $err;
    $solution_2 += cost( $ax, $ay, $bx, $by, $px, $py );
}

say "Solution 1: $solution_1";
say "Solution 2: $solution_2";

sub cost {
    my ( $ax, $ay, $bx, $by, $px, $py ) = @_;

    my $anum  = $by * $px - $bx * $py;
    my $bnum  = -$ay * $px + $ax * $py;
    my $denom = $ax * $by - $ay * $bx;

    if ( $anum % $denom == 0 && $bnum % $denom == 0 ) {
        return ( 3 * $anum + $bnum ) / $denom;
    }
    return 0;
}

# vim:ft=perl:sts=4:sw=4:et
