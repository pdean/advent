#!/usr/bin/env tclsh
# vim:sts=4:sw=4:tw=80:et:ft=tcl 

namespace path [list ::tcl::mathop ::tcl::mathfunc]
package require textutil
namespace import ::textutil::split*

proc run {} {
    set data [read -nonewline stdin]
    set pieces [splitx $data \n\s*\n]
    set rest [lassign $pieces first]
    lassign [split $first :] junk seeds
    set tables [lmap l $rest { lrange [split $l \n] 1 end }]

    foreach {st sz} $seeds {
        set r [list $st [+ $st $sz]]
        set qr [list $r]

        foreach table $tables {
            set qa [list]

            foreach row $table {
                set qn [list]
                lassign $row td ts tl
                set te [+ $ts $tl]

                while {[llength $qr]} {
                    set qr [lassign $qr r]
                    lassign $r ss se

                    set pre  [list $ss [min $ts $se]] 
                    set win  [list [max $ss $ts] [min $te $se]]
                    set pos  [list [max $te $ss] $se]

                    if {[< {*}$pre]} { lappend qn $pre }
                    if {[< {*}$win]} { 
                        lassign $win s e
                        set s [+ $td [- $s $ts]]
                        set e [+ $td [- $e $ts]]
                        lappend qa [list $s $e]
                    }
                    if {[< {*}$pos]} { lappend qn $pos }
                }
                set qr $qn
            }
            set qr [concat $qa $qr]
        }
        lappend mins [min {*}[lmap r $qr {lindex $r 0}]]
    }
    puts [min {*}$mins]
}
run
